/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

/*
 * Copyright 2020 Amazon.com, Inc. or its affiliates. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * or in the "license" file accompanying this file. This file is distributed
 * on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 * express or implied. See the License for the specific language governing
 * permissions and limitations under the License.
 *
 */


buildscript {
    ext {
        opensearch_version = System.getProperty("opensearch.version", "1.0.0")
        kotlin_version = System.getProperty("kotlin.version", "1.4.32")
        junit_version = System.getProperty("junit.version", "5.7.2")
    }

    repositories {
        // For local publish dependency
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.opensearch.gradle:build-tools:${opensearch_version}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlin_version}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.17.1"
        classpath "org.jacoco:org.jacoco.agent:0.8.5"
    }
}

apply plugin: 'base'
apply plugin: 'jacoco'
apply plugin: 'io.gitlab.arturbosch.detekt'
apply from: 'build-tools/merged-coverage.gradle'

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    group = "org.opensearch"
    plugins.withId('java') {
        sourceCompatibility = targetCompatibility = "1.8"
    }
    plugins.withId('org.jetbrains.kotlin.jvm') {
        compileKotlin.kotlinOptions.jvmTarget = compileTestKotlin.kotlinOptions.jvmTarget = "1.8"
        compileKotlin.dependsOn ktlint
    }
}

configurations {
    ktlint
}

dependencies {
    compileOnly "${group}:opensearch:${opensearch_version}"
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    compile "org.jetbrains.kotlin:kotlin-stdlib-common:${kotlin_version}"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3" // ${kotlin_version} does not work for coroutines
    compile "${group}:common-utils:${opensearch_version}.0" // TODO: change to ${opensearch_version}
    compile ("software.amazon.awssdk:ses:2.16.75") {
        exclude module: 'annotations' // conflict with org.jetbrains:annotations, integTestRunner fails with error "codebase property already set"
    }

    testImplementation(
            'org.assertj:assertj-core:3.19.0',
            'org.mockito:mockito-junit-jupiter:3.10.0',
            'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0',
            'io.mockk:mockk:1.11.0',
            'io.mockk:mockk-common:1.11.0',
            'io.mockk:mockk-dsl:1.11.0',
            'io.mockk:mockk-dsl-jvm:1.11.0',
            'io.mockk:mockk-agent-api:1.11.0',
            'io.mockk:mockk-agent-common:1.11.0',
            'io.mockk:mockk-agent-jvm:1.11.0',
            "org.junit.jupiter:junit-jupiter-api:${junit_version}"
    )
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junit_version}")
    testCompile "org.opensearch.test:framework:${opensearch_version}"
    testCompile "org.jetbrains.kotlin:kotlin-test:${kotlin_version}"
    testCompile "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}" // required by mockk
    testCompile "org.mockito:mockito-core:3.10.0"
    testCompile 'com.google.code.gson:gson:2.8.7'
    testImplementation 'org.springframework.integration:spring-integration-mail:5.5.0'
    testImplementation 'org.springframework.integration:spring-integration-test-support:5.5.0'

    ktlint "com.pinterest:ktlint:0.41.0"

    compile project(path: ":${rootProject.name}-spi", configuration: 'shadow')

}

// turn off javadoc as it barfs on Kotlin code
javadoc.enabled = false
licenseHeaders.enabled = true
// no need to validate pom, as we do not upload to maven/sonatype
validateNebulaPom.enabled = false
dependencyLicenses.enabled = false
thirdPartyAudit.enabled = false
// Allow @Test to be used in test classes not inherited from LuceneTestCase.
forbiddenApis.ignoreFailures = true
// Allow test cases to be named Tests without having to be inherited from LuceneTestCase.
testingConventions.enabled = false
loggerUsageCheck.enabled = false

def es_tmp_dir = rootProject.file('build/private/es_tmp').absoluteFile
es_tmp_dir.mkdirs()

afterEvaluate {
    testClusters.integTest.nodes.each { node ->
        def plugins = node.plugins
        def firstPlugin = plugins.get(0)
        plugins.remove(0)
        plugins.add(firstPlugin)
    }
}

test {
    systemProperty 'tests.security.manager', 'false'
    useJUnitPlatform()
}

File repo = file("$buildDir/testclusters/repo")
def _numNodes = findProperty('numNodes') as Integer ?: 1

task integTest(type: RestIntegTestTask) {
    description = "Run tests against a cluster that has security enabled"
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
}
tasks.named("check").configure { dependsOn(integTest) }


integTest {
    if (project.hasProperty('excludeTests')) {
        project.properties['excludeTests']?.replaceAll('\\s', '')?.split('[,;]')?.each {
            exclude "${it}"
        }
    }
    systemProperty 'tests.security.manager', 'false'
    systemProperty 'java.io.tmpdir', es_tmp_dir.absolutePath

    systemProperty "https", System.getProperty("https")
    systemProperty "user", System.getProperty("user")
    systemProperty "password", System.getProperty("password")
    // Tell the test JVM if the cluster JVM is running under a debugger so that tests can use longer timeouts for
    // requests. The 'doFirst' delays reading the debug setting on the cluster till execution time.
    doFirst {
        // Tell the test JVM if the cluster JVM is running under a debugger so that tests can
        // use longer timeouts for requests.
        def isDebuggingCluster = getDebug() || System.getProperty("test.debug") != null
        systemProperty 'cluster.debug', isDebuggingCluster
        // Set number of nodes system property to be used in tests
        systemProperty 'cluster.number_of_nodes', "${_numNodes}"
        // There seems to be an issue when running multi node run or integ tasks with unicast_hosts
        // not being written, the waitForAllConditions ensures it's written
        getClusters().forEach { cluster ->
            cluster.waitForAllConditions()
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "notifications/**/*.kt", "spi/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "notifications/**/*.kt", "spi/**/*.kt"
}

detekt {
    config = files("detekt.yml")
    buildUponDefaultConfig = true
    parallel = true
}

check.dependsOn ktlint

evaluationDependsOnChildren()

check.dependsOn subprojects*.check
