/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    ext {
        opensearch_version = System.getProperty("opensearch.version", "3.4.0-SNAPSHOT")
        isSnapshot = "true" == System.getProperty("build.snapshot", "true")
        buildVersionQualifier = System.getProperty("build.version_qualifier", "")
        // 3.0.0-SNAPSHOT -> 3.0.0.0-SNAPSHOT
        version_tokens = opensearch_version.tokenize('-')
        opensearch_build = version_tokens[0] + '.0'
        plugin_no_snapshot = opensearch_build
        if (buildVersionQualifier) {
            opensearch_build += "-${buildVersionQualifier}"
        }
        if (isSnapshot) {
            opensearch_build += "-SNAPSHOT"
        }
        opensearch_no_snapshot = opensearch_version.replace("-SNAPSHOT","")
        common_utils_version = System.getProperty("common_utils.version", opensearch_build)
        kotlin_version = System.getProperty("kotlin.version", "2.2.20")
        junit_version = System.getProperty("junit.version", "5.7.2")
        aws_version = System.getProperty("aws.version", "1.12.687")
        asm_version = System.getProperty("asm.version", "9.7")
        
        // Check if running on JDK 25 or later
        isJdk25OrLater = JavaVersion.current() >= JavaVersion.VERSION_25
    }

    repositories {
        // For local publish dependency
        mavenLocal()
        mavenCentral()
        maven { url = uri("https://plugins.gradle.org/m2/") }
        maven { url = uri("https://ci.opensearch.org/ci/dbc/snapshots/maven/") }
    }

    dependencies {
        classpath "org.opensearch.gradle:build-tools:${opensearch_version}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlin_version}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.23.0"
        classpath "org.jacoco:org.jacoco.agent:0.8.13"
    }
    
    configurations {
        classpath {
            resolutionStrategy {
                //in order to handle jackson's higher release version in shadow, this needs to be upgraded to latest
                force(group: "org.ow2.asm", name: "asm", version: asm_version)
                force(group: "org.ow2.asm", name: "asm-commons", version: asm_version)
            }
        }
    }
}

apply plugin: 'base'
apply plugin: 'jacoco'

// Only apply detekt if not on JDK 25+
if (!isJdk25OrLater) {
    apply plugin: 'io.gitlab.arturbosch.detekt'
}

apply from: 'build-tools/merged-coverage.gradle'

allprojects {
    version = "${opensearch_build}"

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = uri("https://plugins.gradle.org/m2/") }
        maven { url = uri("https://ci.opensearch.org/ci/dbc/snapshots/maven/") }
    }
    group = "org.opensearch"

    // Only apply Java configuration to subprojects that have the Java plugin
    plugins.withType(JavaPlugin) {
        java {
            sourceCompatibility = JavaVersion.VERSION_21
            targetCompatibility = JavaVersion.VERSION_21
        }
    }
   
    plugins.withId('org.jetbrains.kotlin.jvm') {
        tasks.named('compileJava').configure {
            sourceCompatibility = '21'
            targetCompatibility = '21'
        }
        tasks.named('compileTestJava').configure {
            sourceCompatibility = '21'
            targetCompatibility = '21'
        }
        tasks.named('compileKotlin').configure {
            kotlinOptions.jvmTarget = "21"
            dependsOn ktlint
        }
        tasks.named('compileTestKotlin').configure {
            kotlinOptions.jvmTarget = "21"
        }
    }
}

configurations {
    ktlint {
        resolutionStrategy {
            force "ch.qos.logback:logback-classic:1.5.16"
            force "ch.qos.logback:logback-core:1.5.16"
        }
    }
}

dependencies {
    add("ktlint", "com.pinterest:ktlint:0.47.1") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
        }
    }
}

tasks.register('ktlint', JavaExec) {
    group = "verification"
    description = "Check Kotlin code style."
    mainClass = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "notifications/**/*.kt", "core/**/*.kt", "core-spi/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}

tasks.register('ktlintFormat', JavaExec) {
    group = "formatting"
    description = "Fix Kotlin code style deviations."
    mainClass = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "notifications/**/*.kt", "core/**/*.kt", "core-spi/**/*.kt"
    // https://github.com/pinterest/ktlint/issues/1391
    jvmArgs "--add-opens=java.base/java.lang=ALL-UNNAMED"
}

// Detekt 1.23.0 has a version parsing bug with JDK 25+ (https://github.com/detekt/detekt/issues/8714)
// This is fixed in detekt 2.0+ but not backported to 1.x and detekt 2.0 is in alpha 
// Moreover `2.0.0-alpha.1` artifact is not published to maven 
// For now, we run detekt on JDK 21 and skip it on JDK 25+ to allow CI builds to pass
if (isJdk25OrLater) {
    tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
        enabled = false
    }
    logger.warn("Detekt is disabled on JDK ${JavaVersion.current()} due to known compatibility issues. Use JDK 21 for detekt checks.")
} else {
    detekt {
        config = files("detekt.yml")
        buildUponDefaultConfig = true
        parallel = true
    }
    // Ensure detekt runs with JVM target 21 for consistency
    tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
        jvmTarget = "21"
    }
}

check.dependsOn ktlint

evaluationDependsOnChildren()

check.dependsOn subprojects*.check

// updateVersion: Task to auto increment to the next development iteration
tasks.register('updateVersion') {
    onlyIf { System.getProperty('newVersion') }
    doLast {
        def newVersion = System.getProperty('newVersion')
        println "Setting version to ${newVersion}."
        // String tokenization to support -SNAPSHOT
        def buildFile = file('build.gradle')
        def newVersionValue = newVersion.tokenize('-')[0] + '-SNAPSHOT'
        def updatedContent = buildFile.text.replaceAll(
            /"opensearch\.version", "\d[^"]*"/,
            "\"opensearch.version\", \"${newVersionValue}\""
        )
        buildFile.write(updatedContent)
    }
}
