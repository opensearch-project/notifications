/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */


buildscript {
    ext {
        opensearch_version = System.getProperty("opensearch.version", "3.1.0-SNAPSHOT")
        isSnapshot = "true" == System.getProperty("build.snapshot", "true")
        buildVersionQualifier = System.getProperty("build.version_qualifier", "beta1")
        // 3.0.0-SNAPSHOT -> 3.0.0.0-SNAPSHOT
        version_tokens = opensearch_version.tokenize('-')
        opensearch_build = version_tokens[0] + '.0'
        plugin_no_snapshot = opensearch_build
        if (buildVersionQualifier) {
            opensearch_build += "-${buildVersionQualifier}"
        }
        if (isSnapshot) {
            opensearch_build += "-SNAPSHOT"
        }
        opensearch_no_snapshot = opensearch_version.replace("-SNAPSHOT","")
        common_utils_version = System.getProperty("common_utils.version", opensearch_build)
        kotlin_version = System.getProperty("kotlin.version", "1.9.25")
        junit_version = System.getProperty("junit.version", "5.7.2")
        aws_version = System.getProperty("aws.version", "1.12.687")
        asm_version = System.getProperty("asm.version", "9.7")
    }

    repositories {
        // For local publish dependency
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        classpath "org.opensearch.gradle:build-tools:${opensearch_version}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlin_version}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.23.0"
        classpath "org.jacoco:org.jacoco.agent:0.8.12"
    }
    
     configurations {
        classpath {
            resolutionStrategy {
                //in order to handle jackson's higher release version in shadow, this needs to be upgraded to latest
                force(group: "org.ow2.asm", name: "asm", version: asm_version)
                force(group: "org.ow2.asm", name: "asm-commons", version: asm_version)
            }
        }
    }
}

apply plugin: 'base'
apply plugin: 'jacoco'
apply plugin: 'io.gitlab.arturbosch.detekt'
apply from: 'build-tools/merged-coverage.gradle'

allprojects {
    version = "${opensearch_build}"

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
    }
    group = "org.opensearch"

    plugins.withId('java') {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    plugins.withId('org.jetbrains.kotlin.jvm') {
        compileJava.sourceCompatibility = JavaVersion.VERSION_21
        compileJava.targetCompatibility = JavaVersion.VERSION_21
        compileTestJava.sourceCompatibility = JavaVersion.VERSION_21
        compileTestJava.targetCompatibility = JavaVersion.VERSION_21
        compileKotlin.kotlinOptions.jvmTarget = "21"
        compileTestKotlin.kotlinOptions.jvmTarget = "21"
        compileKotlin.dependsOn ktlint
    }

    configurations {
        agent
    }

    task prepareAgent(type: Copy) {
        from(configurations.agent)
        into "$buildDir/agent"
    }

    dependencies {
        agent "org.opensearch:opensearch-agent-bootstrap:${opensearch_version}"
        agent "org.opensearch:opensearch-agent:${opensearch_version}"
        agent "net.bytebuddy:byte-buddy:1.17.5"
    }

    tasks.withType(Test) {
        dependsOn prepareAgent
        jvmArgs += ["-javaagent:" + project.layout.buildDirectory.file("agent/opensearch-agent-${opensearch_version}.jar").get()]
    }
}

configurations {
    ktlint {
        resolutionStrategy {
            force "ch.qos.logback:logback-classic:1.5.16"
            force "ch.qos.logback:logback-core:1.5.16"
        }
    }
}

dependencies {
    add("ktlint", "com.pinterest:ktlint:0.47.1") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    mainClass = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "notifications/**/*.kt", "core/**/*.kt", "core-spi/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    mainClass = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "notifications/**/*.kt", "core/**/*.kt", "core-spi/**/*.kt"
    // https://github.com/pinterest/ktlint/issues/1391
    jvmArgs "--add-opens=java.base/java.lang=ALL-UNNAMED"
}

detekt {
    config = files("detekt.yml")
    buildUponDefaultConfig = true
    parallel = true
}

check.dependsOn ktlint

evaluationDependsOnChildren()

check.dependsOn subprojects*.check

// updateVersion: Task to auto increment to the next development iteration
task updateVersion {
    onlyIf { System.getProperty('newVersion') }
    doLast {
        ext.newVersion = System.getProperty('newVersion')
        println "Setting version to ${newVersion}."
         // String tokenization to support -SNAPSHOT
        ant.replaceregexp(file:'build.gradle', match: '"opensearch.version", "\\d.*"', replace: '"opensearch.version", "' + newVersion.tokenize('-')[0] + '-SNAPSHOT"', flags:'g', byline:true)
    }
}
