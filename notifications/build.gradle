/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */


buildscript {
    ext {
        opensearch_version = System.getProperty("opensearch.version", "3.4.0-SNAPSHOT")
        isSnapshot = "true" == System.getProperty("build.snapshot", "true")
        buildVersionQualifier = System.getProperty("build.version_qualifier", "")
        // 3.0.0-SNAPSHOT -> 3.0.0.0-SNAPSHOT
        version_tokens = opensearch_version.tokenize('-')
        opensearch_build = version_tokens[0] + '.0'
        plugin_no_snapshot = opensearch_build
        if (buildVersionQualifier) {
            opensearch_build += "-${buildVersionQualifier}"
        }
        if (isSnapshot) {
            opensearch_build += "-SNAPSHOT"
        }
        opensearch_no_snapshot = opensearch_version.replace("-SNAPSHOT","")
        common_utils_version = System.getProperty("common_utils.version", opensearch_build)
        kotlin_version = System.getProperty("kotlin.version", "2.2.20")
        junit_version = System.getProperty("junit.version", "5.7.2")
        aws_version = System.getProperty("aws.version", "1.12.687")
        asm_version = System.getProperty("asm.version", "9.7")
        
        // Check Java version for Detekt compatibility
        javaVersion = JavaVersion.current().majorVersion.toInteger()
        detektEnabled = javaVersion < 25 // Disable Detekt on Java 25+
    }

    repositories {
        // For local publish dependency
        mavenLocal()
        mavenCentral()
        maven { url = uri("https://plugins.gradle.org/m2/") }
        maven { url = uri("https://ci.opensearch.org/ci/dbc/snapshots/maven/") }
    }

    dependencies {
        classpath "org.opensearch.gradle:build-tools:${opensearch_version}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlin_version}"
        // Conditionally add Detekt only if Java version < 25
        if (detektEnabled) {
            classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.23.8"
        }
        classpath "org.jacoco:org.jacoco.agent:0.8.13"
    }
    
     configurations {
        classpath {
            resolutionStrategy {
                //in order to handle jackson's higher release version in shadow, this needs to be upgraded to latest
                force(group: "org.ow2.asm", name: "asm", version: asm_version)
                force(group: "org.ow2.asm", name: "asm-commons", version: asm_version)
            }
        }
    }
}

apply plugin: 'base'
apply plugin: 'jacoco'
// Conditionally apply Detekt plugin only on Java < 25
if (detektEnabled) {
    apply plugin: 'io.gitlab.arturbosch.detekt'
}
apply from: 'build-tools/merged-coverage.gradle'

allprojects {
    version = "${opensearch_build}"

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = uri("https://plugins.gradle.org/m2/") }
        maven { url = uri("https://ci.opensearch.org/ci/dbc/snapshots/maven/") }
    }
    group = "org.opensearch"

    // Only apply Java configuration to projects that have the Java plugin
    plugins.withType(JavaPlugin) {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(25)
            }
            sourceCompatibility = JavaVersion.VERSION_21
            targetCompatibility = JavaVersion.VERSION_21
        }
    }
    
    plugins.withId('org.jetbrains.kotlin.jvm') {
        compileJava.sourceCompatibility = JavaVersion.VERSION_21
        compileJava.targetCompatibility = JavaVersion.VERSION_21
        compileTestJava.sourceCompatibility = JavaVersion.VERSION_21
        compileTestJava.targetCompatibility = JavaVersion.VERSION_21
        compileKotlin.kotlinOptions.jvmTarget = "21"
        compileTestKotlin.kotlinOptions.jvmTarget = "21"
        compileKotlin.dependsOn ktlint
    }
}

configurations {
    ktlint {
        resolutionStrategy {
            force "ch.qos.logback:logback-classic:1.5.16"
            force "ch.qos.logback:logback-core:1.5.16"
        }
    }
}

dependencies {
    add("ktlint", "com.pinterest:ktlint:0.47.1") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    mainClass = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "notifications/**/*.kt", "core/**/*.kt", "core-spi/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    mainClass = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "notifications/**/*.kt", "core/**/*.kt", "core-spi/**/*.kt"
    // https://github.com/pinterest/ktlint/issues/1391
    jvmArgs "--add-opens=java.base/java.lang=ALL-UNNAMED"
}

// Conditionally configure Detekt only if enabled
if (detektEnabled) {
    detekt {
        config = files("detekt.yml")
        buildUponDefaultConfig = true
        parallel = true
    }
    
    tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
        // Detekt should run under Java 25 JVM with JVM target of 17
        jvmTarget = "17"
    }
    
    tasks.withType(io.gitlab.arturbosch.detekt.DetektCreateBaselineTask).configureEach {
        jvmTarget = "17"
    }
    
    // FIX: Reference the detekt TASK, not the detekt extension
    check.dependsOn tasks.named('detekt')
} else {
    logger.warn("⚠️  Detekt is disabled because Java ${javaVersion} is not supported. Detekt requires Java < 25.")
    logger.warn("   See: https://github.com/detekt/detekt/issues/8714")
}

check.dependsOn ktlint

evaluationDependsOnChildren()

check.dependsOn subprojects*.check

// updateVersion: Task to auto increment to the next development iteration
task updateVersion {
    onlyIf { System.getProperty('newVersion') }
    doLast {
        ext.newVersion = System.getProperty('newVersion')
        println "Setting version to ${newVersion}."
         // String tokenization to support -SNAPSHOT
        ant.replaceregexp(file:'build.gradle', match: '"opensearch.version", "\\d.*"', replace: '"opensearch.version", "' + newVersion.tokenize('-')[0] + '-SNAPSHOT"', flags:'g', byline:true)
    }
}
